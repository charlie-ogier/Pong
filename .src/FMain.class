' Gambas class file

siBatL As Short                                                                                 'LH Bat position
siBatR As Short                                                                                 'RH bat position
siRScore As Short                                                                               'RH Score
siLScore As Short                                                                               'LH Score
BatL As Bat                                                                                     'LH Bat
BatR As Bat                                                                                     'RH Bat
bGameEnd As Boolean                                                                             'Game over indicator
bGameStart As Boolean                                                                           'Game start indicator
bNewGame As Boolean                                                                             'New game indicator
bA As Boolean                                                                                   'Control of the 'A' key
bZ As Boolean                                                                                   'Control of the 'Z' key
bUp As Boolean                                                                                  'Control of the 'Up' key
bDown As Boolean                                                                                'Control of the 'Down' key
b1Player As Boolean                                                                             'Number of players, 1 or 2
bExit As Boolean = True                                                                         'To control when the program terminates

Public Sub Form_Open()

SetUpLCDs                                                                                       'Set up the Score displays
Settings.Read(Me, "Window")                                                                     'Get the window settings
TextLabelVer.text = "Version 0.1.0"                                                             'Display the version number

End

Public Sub SetUpLCDs()

LCDLabelLScore.text = Format(0, "000")                                                          'LH Set the score to zero
LCDLabelRScore.text = Format(0, "000")                                                          'RH Set the score to zero

End

Public Sub GamePlay()                                                                           'Let's play!

FMain.Resizable = False                                                                         'Stop the screen being resized during play
bExit = False                                                                                   'Don't allow the program to be closed

While bGameEnd = False                                                                          'While the game is still being played..
  Start                                                                                         'Add another Ball and play..
Wend

bExit = True                                                                                    'Allow the program to be closed
bGameEnd = False                                                                                'Reset variable
FMain.Resizable = True                                                                          'Allow screen resizing
PanelPlayArea.Children.Clear                                                                    'Clear the 'Play area of bats and ball'
StartDisplay                                                                                    'Display the keyboard keys to use

End

Public Sub StartDisplay()                                                                       'Display the keyboard keys to use

Wait                                                                                            'Wiz around the 'Event loop'
With LabelStart                                                                                 'With LabelStart
  .Visible = True                                                                               'Make visible
  .Width = PanelPlayArea.Width                                                                  'Set the width
  .Height = PanelPlayArea.Height                                                                'Set the height
  .X = PanelPlayArea.X                                                                          'Set the horizontal pisition
  .Y = PanelPlayArea.Y                                                                          'Set the vertical position
  .Font.Size = PanelPlayArea.Width / 50                                                         'Set the font height
  .Font.Bold = True                                                                             'Set the font to bold
  .Alignment = Align.Center                                                                     'Align it in the centre
  .Foreground = Color.White                                                                     'Set the font's colour
End With

If ToolButtonOnePlayer.text = "2" Then                                                          'Depending on the 1 or 2 player selected
  LabelStart.Text = "Pong\n\nLeft keys ' A ' & ' Z '\nRight keys [Up Arrow] & [Down Arrow]"     'Set the text
Else
  LabelStart.Text = "Pong\n\nKeys [Up Arrow] & [Down Arrow]"                                    'Set the text
End If 

PanelPlayArea.Background = Color.Black                                                          'Background colour

End

Public Sub Start()                                                                              'Game on..
Dim hBall As Ball                                                                               'The ball
Dim siX, siY, siR1, siR2, siRand, siBounce, siStuck As Short                                    'Various variables
Dim bSwitchLR, bSwitchDown As Boolean                                                           'To control the ball's direct
Dim fSpeed As Float = 0.01

TimerKeys.Start                                                                                 'Controls the key presses for the 'Bats'

bGameStart = True                                                                               'Set bGameStart to True
hBall = New Ball(PanelPlayArea)                                                                 'Add the ball to the 'Play area'
siR1 = Rand(-40, 40)                                                                            'Get a random number between -40 and 40
siR2 = Rand(-40, 40)                                                                            'Get a random number between -40 and 40
siX = ((PanelPlayArea.Width / 2) - (hBall.Width / 2)) + siR1                                    'Get a position that is middle-ish of the 'Play area' widthwise
siY = ((PanelPlayArea.Height / 2) - (hBall.Height / 2)) + siR2                                  'Get a position that is middle-ish of the 'Play area' heightwise
hBall.X = siX                                                                                   'Put the ball in the middle-ish of the 'Play area' widthwise
hBall.Y = siY                                                                                   'Put the ball in the middle-ish of the 'Play area' heightwise

BatL = New Bat(PanelPlayArea)                                                                   'Create the LH Bat
If bNewGame Then siBatL = PanelPlayArea.Height / 2 - (BatL.Height / 2)                          'If it's a new game the get a middle position
BatL.X = 5                                                                                      'Set the Bat just off the LH edge
BatL.Y = siBatL                                                                                 'Set the Bat's position

BatR = New Bat(PanelPlayArea)                                                                   'Create the RH Bat
If bNewGame Then siBatR = PanelPlayArea.Height / 2 - (BatR.Height / 2)                          'If it's a new game the get a middle position
BatR.X = PanelPlayArea.Width - (BatR.Width + 5)                                                 'Set the Bat just off the RH edge
BatR.Y = siBatR                                                                                 'Set the Bat's position
bNewGame = False                                                                                'Set the bNewGame to false. This stops the Bats being repositioned with each new Ball

If b1Player Then                                                                                'If only 1 player mode is selected then..
  BatL.Height = PanelPlayArea.Height                                                            'Make the LH Bat fill the 'Play area' 
  BatL.Y = 0                                                                                    'Position the Bat
  LCDLabelRScore.Visible = False                                                                'Hide the RH score LCD display
Else                                                                                            'Else
  LCDLabelRScore.Visible = True                                                                 'Show the RH score LCD display
End If

LabelStart.Visible = True                                                                       'Show the Start label
LabelStart.y = PanelPlayArea.Y                                                                  'Position it at the top of the 'Play area'
LabelStart.Alignment = Align.Top                                                                'Align the text 'Top'
LabelStart.Text = 3                                                                             'Display "3"
Wait 1                                                                                          'Wait a second
LabelStart.Text = 2                                                                             'Display "2" 
Wait 1                                                                                          'Wait a second
LabelStart.Text = 1                                                                             'Display "1" 
Wait 1                                                                                          'Wait a second
LabelStart.Text = ""                                                                            'Delete the text
LabelStart.Visible = True                                                                       'Hide the Label

siR1 = Rand(0, 1)                                                                               'Get a random number between 0 and 1
siR2 = Rand(0, 1)                                                                               'Get a random number between 0 and 1

If siR1 = 1 Then bSwitchDown = True                                                             'Randomize - Will the ball go up or down?
If siR2 = 1 Then bSwitchLR = True                                                               'Randomize - Will the ball go left or right?

siRand = Rand(0, 2)                                                                             'Set a random value for siRand

Do
  siStuck = siY                                                                                 'To stop the ball just bouncing between the Bats with no need for user input
  If siRand > 4 Then siRand -= Rand(0, 5)                                                       'If the Ball's angle get too great randomize it
  Wait fSpeed                                                                                   'Ball speed control
  If bSwitchDown Then                                                                           'If the Ball is going down then..
    siY += siRand                                                                               'Increase the Ball's vertical position
  Else                                                                                          'Else the Ball is going up so..
    siY -= siRand                                                                               'Dncrease the Ball's vertical position
  End If
  
  If siY >= BatL.Y And siY <= BatL.y + BatL.H And siX < 25 Then                                 'If the Ball 'touches' the LH Bat then
    siRand += Rand(0, 1)                                                                        'Add an extra randon value
    bSwitchLR = True                                                                            'Set switch to True
    fSpeed -= 0.00005                                                                           'Decrease the delay (The Ball will move faster)
  End If 
  
  If siY >= PanelPlayArea.Height - hBall.Height Then bSwitchDown = False                        'If the Ball has reached the top of the 'Play area' change its direction
  If siY <= 0 Then bSwitchDown = True                                                           'If the Ball has reached the bottom of the 'Play area' change its direction
 
  If bSwitchLR Then                                                                             'If the Ball is going right then..
    siX += 5                                                                                    'Increase the Ball's hortzontal position
  Else                                                                                          'Else the Ball is going left so..
    siX -= 5                                                                                    'Decrease the Ball's hortzontal position
  End If

  If siY >= BatR.Y And siY <= BatR.y + BatR.H And siX > PanelPlayArea.Width - (BatR.Width + 5 + hBall.Width) Then 'If the Ball 'touches' the RH Bat then
    siRand += Rand(0, 1)                                                                        'Add an extra random value
    bSwitchLR = False                                                                           'Change the Ball's direction
    fSpeed -= 0.00005                                                                           'Decrease the delay (The Ball will move faster)
  End If 
  
  If siX < -10 Then                                                                             'If the Ball was not 'Batted' by the LH Bat then..
    Inc siLScore                                                                                'Increase the RH score
    If siLScore > 998 Then bGameEnd = True                                                      'If you get to 999 that's enough!
    Break                                                                                       'Get out of here
  Endif

  If siX > PanelPlayArea.Width + 10 Then                                                        'If the Ball was not 'Batted' by the RH Bat then..
    Inc siRScore                                                                                'Increase the LH score
    If siLScore > 998 Then bGameEnd = True                                                      'If you get to 999 that's enough!
    Break                                                                                       'Get out of here
  Endif 

  hBall.Y = siY                                                                                 'Set Ball position (Vertical)
  hBall.X = siX                                                                                 'Set Ball position (Horizontal)
  BatL.Y = siBatL                                                                               'Set the LH Bat position (Vertical)
  BatR.Y = siBatR                                                                               'Set the RH Bat position (Vertical)
  PanelPlayArea.Background = Color.Black                                                        'Set the 'Play area' background to black

 Wait                                                                                           'Wiz around the 'Event loop'
 
 If bGameEnd = True Then                                                                        'If the game is over
   Stop Event                                                                                   'Stop any Event
   Break                                                                                        'Get out of here
 Endif

If siStuck = siY Then siRand += 1                                                               'If the Ball is 'stuck' bouncing horizontally then change the angle

Loop                                                                                            'End of Do..Loop

TimerKeys.Stop                                                                                  'Stop reading the key presses

bGameStart = False                                                                              'Set bGameStart to False
PanelPlayArea.Children.clear                                                                    'Clear the 'Play area' of Bats and Ball

LCDLabelLScore.text = Format(siRScore, "000")                                                   'Display the score
LCDLabelRScore.text = Format(siLScore, "000")                                                   'Display the score

Wait                                                                                            'Wiz around the 'Event loop'

End

Public Sub Form_KeyPress()                                                                      'To capture the users key presses
Dim iCode As Integer = Key.Code                                                                 'Store the pressed key

If bGameStart = False Then Return                                                               'If the game has not started, get out of here

If iCode = Key.Esc Then                                                                         'If the [Esc] key is pressed then..
  bGameEnd = True                                                                               'Set bGameEnd to True
  PanelTools.Enabled = True                                                                     'Enable the Tools Panel
End If

If iCode = Key["A"] Then bA = True                                                              'If the "A" key is pressed set bA to True
If iCode = Key["Z"] Then bZ = True                                                              'If the "Z" key is pressed set bZ to True
If iCode = Key.Up Then bUp = True                                                               'If the "Up" key is pressed set bUp to True
If iCode = Key.Down Then bDown = True                                                           'If the "Down" key is pressed set bDown to True

End

Public Sub Form_KeyRelease()                                                                    'To capture the users key releases
Dim iCode As Integer = Key.Code                                                                 'Store the pressed key

If bGameStart = False Then Return                                                               'If the game has not started, get out of here

If iCode = Key.Esc Then bGameEnd = False                                                        'If the [Esc] key is released then set bGameEnd to False
If iCode = Key["A"] Then bA = False                                                             'If the "A" key is released set bA to False
If iCode = Key["Z"] Then bZ = False                                                             'If the "Z" key is released set bZ to False
If iCode = Key.Up Then bUp = False                                                              'If the "Up" key is released set bUp to False
If iCode = Key.Down Then bDown = False                                                          'If the "Down" key is released set bDown to False

End

Public Sub TimerKeys_Timer()                                                                    'Timer to move the Bats
Dim byMove As Byte = 8                                                                          'Amount of Bat movement

If bA Then siBatL -= byMove                                                                     'If bA is True move the LH Bat up
If bZ Then siBatL += byMove                                                                     'If bZ is True move the LH Bat down
If bUp Then siBatR -= byMove                                                                    'If bUp is True move the RH Bat up
If bDown Then siBatR += byMove                                                                  'If bDown is True move the RH Bat down

If siBatL < 0 Then siBatL = 0                                                                   'If the LH Bat is at the top of the area stop it running off the screen
If siBatL > PanelPlayArea.Height - BatL.Height Then siBatL = PanelPlayArea.Height - BatL.Height 'If the LH Bat is at the bottom of the area stop it running off the screen
If siBatR < 0 Then siBatR = 0                                                                   'If the RH Bat is at the top of the area stop it running off the screen
If siBatR > PanelPlayArea.Height - BatR.Height Then siBatR = PanelPlayArea.Height - BatR.Height 'If the RH Bat is at the bottom of the area stop it running off the screen

End

Public Sub Form_Resize()

PanelPlayArea.Children.Clear                                                                    'Clear the 'Play area'
HBoxControl.Width = FMain.Width                                                                 'Set the width of the HBoxControl
HBoxControl.Height = 32                                                                         'Set the height of the HBoxControl
PanelPlayArea.Y = 32                                                                            'Set the 'Play area' horizontal position
PanelPlayArea.X = 0                                                                             'Set the 'Play area' vertical position
PanelPlayArea.Height = FMain.Height - 32                                                        'Set the 'Play area' height
PanelPlayArea.Width = FMain.Width                                                               'Set the 'Play area' width
StartDisplay                                                                                    'Run the StartDisplay routine

End

Public Sub AllToolButtons_Click(Optional sKey As String)                                        'To monitor all the toolbar buttons (Optional is for later use)

If Not sKey Then sKey = Last.name                                                               'If sKey is empty then sKey = the name of the toolbar button that was clicked

Select Case sKey                                                                                'Depending what's in sKey..
  Case "ToolButtonStop"                                                                         'If it's the 'Stop' button then..
    PanelTools.Enabled = True                                                                   'Enable the Tool Panel
    bGameEnd = True                                                                             'Set bEndGame to True
  Case "ToolButtonPlay"                                                                         'If it's the 'Play' button then..
    PanelTools.Enabled = False                                                                  'Disable the Tool Panel
    siLScore = 0                                                                                'Set the LH score to 0
    siRScore = 0                                                                                'Set the RH score to 0
    SetUpLCDs                                                                                   'Set up the LCD score displays
    bGameStart = True                                                                           'Set bGameStart to True
    LabelStart.Visible = False                                                                  'Hide the LabelStart
    PanelPlayArea.Children.clear                                                                'Clear the 'Play area'
    Wait                                                                                        'Wiz around the 'Event loop'
    bNewGame = True                                                                             'Set bNewGame to True
    GamePlay                                                                                    'Run the GamePlay routine
  Case "ToolButtonAbout"                                                                        'If it's the 'About' button then..
    About.ShowModal                                                                             'Show the about Form
  Case "ToolButtonExit"                                                                         'If it's the 'Exit' button then..
    bExit = True                                                                                'Allow the program to close
    Me.Close                                                                                    'Close the Form
  Case "ToolButtonOnePlayer"                                                                    'If it's the 'One Player' button then..
    b1Player = Not b1Player                                                                     'Reverse b1Player value
    If b1Player Then                                                                            'If b1Player is True then..
      ToolButtonOnePlayer.Text = "1"                                                            'Set the button text to '1'
      StartDisplay                                                                              'Run the StartDisplay routine
    Else                                                                                        'Else
      ToolButtonOnePlayer.Text = "2"                                                            'Set the button text to '2'
      StartDisplay                                                                              'Run the StartDisplay routine
    Endif
End Select

End

Public Sub Form_Close()                                                                         'When the program closes

If Not bExit Then Stop Event                                                                    'If bExit is False then cancel the close Event 
Settings.Write(Me, "Window")                                                                    'Save the Forms layout on the screen

End

Public Sub Timer1_Timer()                                                                       'Timer (There must be a better way to do this!)

Timer1.Stop                                                                                     'Stop the Timer
LabelStart.Visible = True                                                                       'Make the LabelStart visible
StartDisplay                                                                                    'Run the StartDisplay routine

End