' Gambas class file

' Added keyboard speed settings and delay controls to improve bat response

Public siBatL As Short
Public siBatR As Short
Public siRScore As Short
Public siLScore As Short
Public BatL As Bat
Public BatR As Bat
Public bGameEnd As Boolean
Public bGameStart As Boolean
Public bNewGame As Boolean

Public Sub Form_Open()

Me.Background = Color.Black
LCDLabelLScore.text = Format(0, "000")
LCDLabelRScore.text = Format(0, "000")
Settings.Read(Me, "Window")
Timer1.start

End

Public Sub GamePlay()

GetExistingKeyboardDelayAndRate
SetNewKeyboardDelayAndRate
While bGameEnd = False
  Start
Wend
ResetKeyboardDelayAndRate
bGameEnd = False

PanelPlayArea.Children.Clear
StartDisplay

End

Public Sub StartDisplay()

Wait
With LabelStart
  .Visible = True
  .Width = PanelPlayArea.Width
  .Height = PanelPlayArea.Height
  .X = PanelPlayArea.X
  .Y = PanelPlayArea.Y
End With

End

Public Sub Start()
Dim hBall As Ball
Dim siX, siY, siR1, siR2 As Short
Dim bSwitchLR, bSwitchDown As Boolean

bGameStart = True
hBall = New Ball(PanelPlayArea)
hBall.Tag = "Remove"
siR1 = Rand(-40, 40)
siR2 = Rand(-40, 40)
siX = ((PanelPlayArea.Width / 2) - (hBall.Width / 2)) + siR1
siY = ((PanelPlayArea.Height / 2) - (hBall.Height / 2)) + siR2
Label1.text = "Ball start pos : X = " & Str(siX) & " Y = " & Str(siY)
hBall.X = siX
hBall.Y = siY

BatL = New Bat(PanelPlayArea)
If bNewGame Then siBatL = PanelPlayArea.Height / 2 - (BatL.Height / 2)
BatL.X = 5
BatL.Y = siBatL

BatR = New Bat(PanelPlayArea)
If bNewGame Then siBatR = PanelPlayArea.Height / 2 - (BatR.Height / 2)
BatR.X = PanelPlayArea.Width - (BatR.Width + 5)
BatR.Y = siBatR
bNewGame = False

Wait 2

siR1 = Rand(0, 1)
siR2 = Rand(0, 1)

If siR1 = 1 Then bSwitchDown = True
If siR2 = 1 Then bSwitchLR = True

Do
  Wait 0.002
  If bSwitchDown Then 
    Inc siY
  Else
    Dec siY
  End If
  
  If siY >= BatL.Y And siY <= BatL.y + BatL.H And siX < 25 Then 
    bSwitchLR = True 
  End If 
  If siY >= PanelPlayArea.Height Then bSwitchDown = False 
  If siY = 0 Then bSwitchDown = True
  
  If bSwitchLR Then 
    Inc siX
  Else
    Dec siX
  End If
  
  If siY >= BatR.Y And siY <= BatR.y + BatR.H And siX > PanelPlayArea.Width - (BatR.Width + 5 + hBall.Width) Then bSwitchLR = False
  If siX < -5 Then 
    Inc siLScore
    Break
  Endif

  If siX > PanelPlayArea.Width + 5 Then
    Inc siRScore
    Break
  Endif 
  hBall.Y = siY
  hBall.X = siX
  BatL.Y = siBatL
  BatR.Y = siBatR
  PanelPlayArea.Background = Color.Black
 Wait
 If bGameEnd = True Then 
   Stop Event
   Break
 Endif

Loop

bGameStart = False
PanelPlayArea.Children.clear

LCDLabelRScore.Text = Format(siLScore, "000")
LCDLabelLScore.Text = Format(siRScore, "000")

Wait

End

Public Sub Form_KeyPress()
Dim iCode As Integer = Key.Code

If bGameStart = False Then Return

If iCode = Key.Esc Then bGameEnd = True
If iCode = Key["A"] Then siBatL -= 10
If iCode = Key["Z"] Then siBatL += 10
If iCode = Key.Up Then siBatR -= 10
If iCode = Key.Down Then siBatR += 10

If siBatL < 0 Then siBatL = 0
If siBatL > PanelPlayArea.Height - BatL.Height Then siBatL = PanelPlayArea.Height - BatL.Height
If siBatR < 0 Then siBatR = 0
If siBatR > PanelPlayArea.Height - BatR.Height Then siBatR = PanelPlayArea.Height - BatR.Height

End

Public Sub Form_Resize()

HBoxControl.Width = FMain.Width
HBoxControl.Height = 32
PanelPlayArea.Y = 32
PanelPlayArea.X = 0
PanelPlayArea.Height = FMain.Height - 32
PanelPlayArea.Width = FMain.Width

End

Public Sub AllToolButtons_Click()

Select Case Last.name
  Case "ToolButtonStop" 
    bGameEnd = True
  Case "ToolButtonPlay"
    siLScore = 0
    siRScore = 0
    LCDLabelLScore.text = Format(0, "000")
    LCDLabelRScore.text = Format(0, "000")
    bGameStart = True
    LabelStart.Visible = False
    PanelPlayArea.Children.clear
    Wait
    bNewGame = True
    GamePlay
  Case "ToolButtonAbout"
    About.ShowModal
End Select

End

Public Sub ResetKeyboardDelayAndRate()

Shell "xset r rate " & Settings["Delay"] & " " & Settings["Rate"]

End

Public Sub SetNewKeyboardDelayAndRate()

Shell "xset r rate 100 52"

End

Public Sub GetExistingKeyboardDelayAndRate()

Dim sKeyb, sTemp, sChar As String
Dim siCount, siX, siChar As Short
Dim siRate As New Short[]

Shell "xset -q" To sKeyb

For Each sTemp In Split(sKeyb, gb.NewLine)
  If InStr(sTemp, "auto repeat delay:") Then Break 'sRate = sTemp
Next

For siCount = InStr(sTemp, ":") + 1 To Len(sTemp)
  siChar = Asc(Mid(sTemp, siCount, 1))
  If siChar > 47 And siChar < 58 Then sChar &= Mid(sTemp, siCount, 1)
  If siChar = 58 And siX = 0 Then 
    siRate.add(Val(sChar))
    sChar = ""
    Inc siX
    Continue
  End If
Next

siRate.add(Val(sChar))

Settings["Delay"] = siRate[0]
Settings["Rate"] = siRate[1]

End

Public Sub Form_Close()

ResetKeyboardDelayAndRate
Settings.Write(Me, "Window")

End

Public Sub Timer1_Timer()

Timer1.Stop
LabelStart.Visible = True
StartDisplay

End